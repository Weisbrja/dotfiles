#!/bin/zsh

# cursor shapes for vim modes
cursor_block='\e[1 q'
cursor_beam='\e[5 q'
function zle-keymap-select {
	case "$KEYMAP" in
		vicmd) echo -ne "$cursor_block" ;;
		viins|main) echo -ne "$cursor_beam" ;;
	esac
}
zle -N zle-keymap-select

# automatically correct spelling mistakes
auto-correct() {
	zle .spell-word
	zle .$WIDGET
}
zle -N accept-line auto-correct
zle -N magic-space auto-correct
bindkey ' ' magic-space

function precmd {
	# set terminal title to current working directory
	print -Pn '\e]0;%~\a'
}

function preexec {
	# set terminal title to current command
	print -Pn "\e]0;$1\a"

	# reset cursor shape
	echo -ne "$cursor_beam"
}

zle-line-init() {
	# reset cursor shape
	echo -ne "$cursor_beam"
}
zle -N zle-line-init
